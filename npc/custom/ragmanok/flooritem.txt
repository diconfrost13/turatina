//==== Gidz Scripts =============================================
//= @flooritem
//===== By: =====================================================
//= Gidz
//===== Current Version: ========================================
//= 1.0
//===== Description: ============================================
//= @itemshower
//===== Changelog: ==============================================
//= 1.0 Implementation
//===============================================================

-	script	Item_Shower	-1,{
OnInit:
	bindatcmd "flooritem", strnpcinfo(0) +"::OnItemShower", 99,99;
	end;
	
OnItemShower:
	.@itemid = atoi( .@atcmd_parameters$[0] );
	.@amount = atoi( .@atcmd_parameters$[1] );
	if ( getitemname( .@itemid ) == "null" ) {
		message strcharinfo(0), "Invalid Item ID";
		end;
	}
	if ( !.@amount || .@amount > 500 ) .@amount = 500;
	getmapxy .@map$, .@x, .@y, BL_PC;
	while ( .@i < .@amount ) {
		do {
			.@dx = rand( -14, 14 );
			.@dy = rand( -14, 14 );
		} while( !checkcell( .@map$,(.@x+.@dx),(.@y+.@dy),cell_chkpass ) );
		makeitem .@itemid, 1, .@map$, .@x + .@dx, .@y + .@dy;
		.@i++;
	}
	end;
}